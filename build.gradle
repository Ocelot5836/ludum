plugins {
    id "java"
    id "xyz.jpenilla.run-paper" version "1.0.4"
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id 'maven-publish'
}

version = "1.1.0"
group = "io.github.ocelot"
archivesBaseName = "ludum"

configurations {
    shadow
}

tasks {
    runServer {
        minecraftVersion("1.17.1")
    }
}

repositories {
    maven { url "https://papermc.io/repo/repository/maven-public/" }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    implementation "co.aikar:acf-paper:0.5.0-SNAPSHOT"
    shadow "co.aikar:acf-paper:0.5.0-SNAPSHOT"
    implementation "io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT"
}

jar {
    classifier "slim"
}

java {
    withSourcesJar()
}

shadowJar {
    configurations = [project.configurations.shadow]
    relocate 'co.aikar.commands', 'io.github.ocelot.minigame.acf'
    relocate 'co.aikar.locales', 'io.github.ocelot.minigame.locales'
    classifier ""
}

build.dependsOn("shadowJar")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
            artifact(sourcesJar) {
                classifier "sources"
            }
        }
    }
    repositories {
        if (project.hasProperty("jacksonMavenUser") && project.hasProperty("jacksonMavenPass")) {
            maven {
                url "https://maven.jaackson.me"
                credentials {
                    username = project.properties.jacksonMavenUser
                    password = project.properties.jacksonMavenPass
                }
            }
        }
    }
}
